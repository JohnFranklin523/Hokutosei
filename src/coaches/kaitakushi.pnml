// Begin Kaitakushi

// Graphics

// Purchase Menu
spriteset (spriteset_kaitakushi_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "kaitakushi")
}

// Vehicle
spriteset (spriteset_kaitakushi_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "kaitakushi")
}

switch (FEAT_TRAINS, SELF, switch_kaitakushi_articulated_part, extra_callback_info1) {
    1..2: return kaitakushi;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_kaitakushi_graphics, position_in_articulated_veh % 3) {
    1: spriteset_kaitakushi_original;
    empty_sprites;   
}

switch (FEAT_TRAINS, SELF, switch_kaitakushi_capacity, position_in_articulated_veh % 3) {
    1: return 44;
    return 0;
}


item (FEAT_TRAINS, kaitakushi) {
    property {
        // Menu
        name:                           string(STR_NAME_KAITAKUSHI);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        
        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1880,01,01);
        model_life:                     50;
        vehicle_life:                   30;
        track_type:                     NAAN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 44;
        loading_speed:                  5 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          80 km/h;
        power:                          0 hp;
        weight:                         8 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_KAITAKUSHI_LIVERY_AVAILABILITY), string(STR_COMFORT_LOW, 144), string(STR_NO_AIR_CONDITIONER));
        can_attach_wagon:               locowagon;
        
        // Graphics
        default:                        switch_kaitakushi_graphics;
        purchase:                       spriteset_kaitakushi_purchase_original;
        articulated_part:               switch_kaitakushi_articulated_part;

        // Stats
        length:                         switch_length_1_4_1;
        cargo_capacity:                 switch_kaitakushi_capacity;
        cargo_age_period:               (9 * 4 * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    2;
        running_cost_factor:            4 * runningcostfactor() * cafecostfactor(); // NO AC FUNCTION
        purchase_running_cost_factor:   8;
        purchase_cargo_capacity:        44/3;
    }
}

// End Kaitakushi
